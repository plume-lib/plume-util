plugins {
  // To make build/libs/options-all.jar, run: gradle shadowJar
  id 'com.github.johnrengelman.shadow' version '2.0.4'
  // Run the Error Prone linter.
  id "net.ltgt.errorprone-base" version "0.0.16"
  // Commented out because Google Java Format mis-formats type annotations.
  // // Code formatting with Google Java Format; use targets "goJF" and "verJF".
  // id "com.github.sherter.google-java-format" version "0.7.1"
}

apply plugin: 'java-library'

// Checker Framework pluggable type-checking
apply from: 'gradle/checkerframework-annos-in-comments.gradle'

// To upload to Maven Central:
//  * update version number in gradle/mavencentral.gradle
//  * run:  ./gradlew javadocWeb uploadArchives
//  * browse to https://oss.sonatype.org/#stagingRepositories
apply from: 'gradle/mavencentral.gradle'

dependencies {
  testImplementation 'junit:junit:4.12'
  implementation 'org.plumelib:bcel-util:0.0.9' // for JvmUtil.binaryNameToClassGetName(String)
  errorprone 'com.google.errorprone:error_prone_core:2.3.1'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

// Turn Javadoc warnings into errors.
javadoc {
  options.addStringOption('Xwerror', '-Xdoclint:all')
  // Can't enable private because WeakIdentityHashMap.java doesn't have exhaustive Javadoc comments
  // options.addStringOption('private', '-quiet')
}

task javadocWeb(type: Javadoc) {
  source = sourceSets.main.allJava
  destinationDir = file("/cse/web/research/plumelib/plume-util/api")
  classpath = project.sourceSets.main.compileClasspath
}

// `requireJavadoc` target ensures that all Java elements have Javadoc documentation.
configurations {
  requireJavadoc
}
dependencies {
  compile group: 'org.plumelib', name: 'require-javadoc', version: '0.0.6'
}
task requireJavadoc(type: Javadoc) {
  destinationDir.deleteDir()
  source = sourceSets.main.allJava
  classpath = project.sourceSets.main.compileClasspath
  // TODO: uncomment.  "requireJavadoc" has no effect over "javadoc" while this line is commented.
  options.memberLevel = JavadocMemberLevel.PRIVATE
  options.docletpath = project.sourceSets.main.compileClasspath as List
  options.doclet = "org.plumelib.javadoc.RequireJavadoc"
  options.addStringOption('skip', 'WeakIdentityHashMap|WeakHasherMap')
}

/* Make Emacs TAGS table */
task tags(type: Exec) {
  description "Run etags to create an Emacs TAGS table"
  commandLine "bash", "-c", "find src/ -name '*.java' | sort | xargs etags"
}
